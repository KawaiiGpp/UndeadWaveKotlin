plugins {
    id 'org.jetbrains.kotlin.jvm' version '2.1.20'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'com.akira'
version = '1.0.0'

def serverFolder = 'D:/Server/PaperDebug'
def libsFolder = serverFolder + '/libraries'
def versionsFolder = serverFolder + '/versions'
def pluginsFolder = serverFolder + '/plugins'
def coreFile = 'AkiraCoreKotlin.jar'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.jetbrains.kotlin:kotlin-test'

    compileOnly fileTree(dir: libsFolder, include: ['**/*.jar'])
    compileOnly fileTree(dir: versionsFolder, include: ['**/*.jar'])
    compileOnly files(pluginsFolder + '/' + coreFile)
}

shadowJar {
    archiveClassifier.set('shadow')

    dependencies {
        exclude(dependency('org.jetbrains:annotations'))
        exclude(dependency('org.jetbrains.kotlin:kotlin-stdlib'))
    }

    relocate 'kotlin', 'com.akira.shadow.kotlin'
}

tasks.register('deployPlugin', Copy) {
    dependsOn shadowJar

    from tasks.shadowJar.getArchiveFile()
    into pluginsFolder
    rename { project.name + '.jar' }
}

tasks.build {
    dependsOn tasks.named('deployPlugin')
}

processResources {
    inputs.property("checkVersion", project.version)

    filesMatching('plugin.yml') {
        expand(version: project.version)
    }
}

test {
    useJUnitPlatform()
}

kotlin {
    jvmToolchain(17)
}